---
page_title: "Resource: octopusdeploy_kubernetes_monitor"
description: |-
  Manages a Kubernetes Monitor in Octopus Deploy.
---

# Resource: octopusdeploy_kubernetes_monitor

Manages a Kubernetes monitor in Octopus Deploy. The Kubernetes monitor provides observability capabilities for Kubernetes deployments.

~> **Note** This resource requires Octopus Server 2025.3 or later.

~> **Important** The `machine_id` must reference a Kubernetes Agent deployment target. Regular deployment targets or workers are not supported.

## Example Usage

### Basic Kubernetes Monitor with Kubernetes Agent

This example shows how to set up a Kubernetes monitor along with the Kubernetes Agent using Helm:

```hcl
locals {
  octopus_api_key         = "API-XXXXXXXXXXXXXXXX"
  octopus_address         = "https://myinstance.octopus.app"
  octopus_grpc_address    = "grpc://myinstance.octopus.app:8443"
  octopus_polling_address = "https://polling.myinstance.octopus.app/"
}

data "octopusdeploy_teams" "everyone" {
  partial_name = "Everyone"
  skip         = 0
  take         = 1
}

resource "octopusdeploy_space" "monitoring" {
  name                 = "Kubernetes Terraform Examples"
  description          = "Terraform created examples"
  space_managers_teams = [data.octopusdeploy_teams.everyone.teams[0].id]
}

resource "octopusdeploy_environment" "example" {
  name        = "Example"
  space_id    = octopusdeploy_space.monitoring.id
}

resource "octopusdeploy_polling_subscription_id" "agent_subscription_id" {}
resource "octopusdeploy_tentacle_certificate" "agent_cert" {}

# Create the Kubernetes agent deployment target
resource "octopusdeploy_kubernetes_agent_deployment_target" "example" {
  name         = "Example Kubernetes Agent"
  space_id     = octopusdeploy_space.monitoring.id
  environments = [octopusdeploy_environment.example.id]
  roles        = ["k8s-agent-example"]

  thumbprint = octopusdeploy_tentacle_certificate.agent_cert.thumbprint
  uri        = octopusdeploy_polling_subscription_id.agent_subscription_id.polling_uri
}

# Generate a unique installation ID for the monitor
resource "random_uuid" "monitor_installation" {}

# Create the Kubernetes monitor
resource "octopusdeploy_kubernetes_monitor" "example" {
  space_id        = octopusdeploy_space.monitoring.id
  installation_id = random_uuid.monitor_installation.result
  machine_id      = octopusdeploy_kubernetes_agent_deployment_target.example.id
}

# Install the Kubernetes agent and monitor via Helm
resource "helm_release" "kubernetes_agent" {
  name             = "example-kubernetes-agent"
  repository       = "oci://registry-1.docker.io"
  chart            = "octopusdeploy/kubernetes-agent"
  version          = "2.*.*"
  atomic           = true
  create_namespace = true
  namespace        = "octopus-agent-example"

  set = [
    {
      name  = "agent.acceptEula"
      value = "Y"
    },
    {
      name  = "agent.name"
      value = octopusdeploy_kubernetes_agent_deployment_target.example.name
    },
    {
      name  = "agent.serverUrl"
      value = local.octopus_address
    },
    {
      name  = "agent.serverCommsAddress"
      value = local.octopus_polling_address
    },
    {
      name  = "agent.serverSubscriptionId"
      value = octopusdeploy_polling_subscription_id.agent_subscription_id.polling_uri
    },
    {
      name  = "agent.space"
      value = octopusdeploy_space.monitoring.name
    },
    {
      name  = "agent.deploymentTarget.enabled"
      value = "true"
    },
    {
      name  = "agent.targetName"
      value = octopusdeploy_kubernetes_agent_deployment_target.example.name
    },
    {
      name  = "kubernetesMonitor.enabled"
      value = "true"
    },
    {
      name  = "kubernetesMonitor.registration.register"
      value = "false"
    },
    {
      name  = "kubernetesMonitor.monitor.serverGrpcUrl"
      value = local.octopus_grpc_address
    },
    {
      name  = "kubernetesMonitor.monitor.installationId"
      value = octopusdeploy_kubernetes_monitor.example.installation_id
    }
  ]

  set_sensitive = [
    {
      name  = "agent.serverApiKey"
      value = local.octopus_api_key
    },
    {
      name  = "agent.certificate"
      value = octopusdeploy_tentacle_certificate.agent_cert.base64
    },
    {
      name  = "kubernetesMonitor.monitor.authenticationToken"
      value = octopusdeploy_kubernetes_monitor.example.authentication_token
    }
  ]

  set_list = [
    {
      name  = "agent.deploymentTarget.initial.environments"
      value = octopusdeploy_kubernetes_agent_deployment_target.example.environments
    },
    {
      name  = "agent.deploymentTarget.initial.tags"
      value = octopusdeploy_kubernetes_agent_deployment_target.example.roles
    }
  ]
}
```
<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `installation_id` (String) A unique identifier for this Kubernetes monitor installation.
- `machine_id` (String) The ID of the Kubernetes Agent deployment target machine.

### Optional

- `preserve_authentication_token` (Boolean) Controls whether the authentication token should be preserved during re-registration. If not supplied (null), or false, the token will be regenerated (default behavior). If true, the existing token will be preserved.
- `space_id` (String) The space ID associated with this resource.

### Read-Only

- `authentication_token` (String, Sensitive) The authentication token for the Kubernetes monitor. This is generated by Octopus and should be stored securely. If preserve_authentication_token is set, a null value will be set.
- `certificate_thumbprint` (String) The certificate thumbprint used to verify the connection to the Octopus server.
- `id` (String) The unique ID for this resource.